// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.0.10"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'java'

repositories {
    mavenCentral()
    maven { url = "http://tehnut.info/maven" }
    maven { url = "http://maven.tterrag.com" }
    maven { url = "http://dvs1.progwml6.com/files/maven" }
    maven { url = "http://files.minecraftforge.net/maven" }
    maven { url = "http://maven.cil.li/" }
    maven { url = "http://maven.ic2.player.to" }
    maven { url = "http://maven.mcmoddev.com" }
    maven { url = "http://mod-buildcraft.com/maven/" }
    ivy {
        url "http://ae-mod.info/builds"
        layout "pattern", {
            artifact "[module]-[revision](-[classifier])(.[ext])"
        }
    }
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // the repo from which to get NEI and stuff
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven { url "http://maven.ironhide.de/" }
    maven { url = "http://dl.tsr.me/artifactory/libs-release-local" }
    maven { // CoFH
        name = "Covers Maven"
        url = "http://maven.covers1624.net"
    }
    /*ivy {
        url "http://ae-mod.info/builds"
        layout "pattern", {
            artifact "[module]-[revision](-[classifier])(.[ext])"
        }
    } */
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// define the properties file
ext.configFile = file "project.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
config = project.ext.config


version = "${config.mod_version}"
if (hasProperty('appendBuildNumber')) {
    version = version + "-" + appendBuildNumber.toString()
} else {
    version = version + "-0"
}

group = "${config.group_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${config.mod_id}-${config.minecraft_version}"

minecraft {
    version = "${config.forge_version}"
    runDir = "run"

    replace "@VERSION@", project.version
    replace "omlib@[0.0.0,)", "omlib@[" + config.omlib_version + ",)"
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171212"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

configurations {
    runtime.exclude group: 'ValkyrienWarfareRevamped', module: 'valkyrienwarfare'
}

def omlib_local = false
if (project.allprojects.findAll({ it.name == "OMLib" }).size() > 0) {
    omlib_local = true
}
if (rootProject.name == "OMTControl") {
    omlib_local = true
}
def ompd_local = false
if (project.allprojects.findAll({ it.name == "OpenModularPassiveDefense" }).size() > 0) {
    ompd_local = true
}

dependencies {
    compileOnly "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}:api"
    compileOnly "mcp.mobius.waila:Hwyla:${config.hwyla_version}_${config.minecraft_version_short}"
    compileOnly "cofh:RedstoneFlux:${config.minecraft_version_short}-${config.redstoneflux_version}:deobf"
    compileOnly "mcjty.theoneprobe:TheOneProbe-${config.minecraft_version_short}:${config.minecraft_version_short}-${config.top_version}"
    compileOnly "li.cil.oc:OpenComputers:MC${config.minecraft_version_short}.1-${config.oc_version}"
    compileOnly "net.industrial-craft:industrialcraft-2:${config.ic2_version}:dev"
    compileOnly "net.darkhax.tesla:Tesla-${config.minecraft_version_short}:${config.tesla_version}"
    //compileOnly "ValkyrienWarfareRevamped:valkyrienwarfare:${config.valkyrienwarfare_version}:dev"
    if (!omlib_local) {
        compileOnly "omtteam:omlib:${config.minecraft_version}-${config.omlib_version}:dev"
    } else {
        compile project(':OMLib')
    }
    if (ompd_local) {
        runtime project(':OpenModularPassiveDefense')
    }
    //runtime "com.enderio:EnderIO:${config.minecraft_version}-${config.enderio_version}"
    runtime "com.enderio.core:EnderCore:${config.minecraft_version}-${config.endercore_version}"
    //runtime "com.github.azanor:baubles:${config.baubles_version}"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

tasks.build.dependsOn deobfJar, sourceJar

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            groupId config.group_prefix
            version config.minecraft_version + "-" + project.version
            artifactId config.mod_id
            artifact jar
            artifact sourceJar
            artifact deobfJar
        }
    }
    repositories {
        mavenLocal()
    }
}

//// DETECTING COMMIT NAME AND ACTIVATING CURSE RELEASE \\\\

def commit_release_type = getCommmitReleaseType()
def getCommmitReleaseType(){
    if(project.hasProperty("curseforge_key")){
        def stdout = new ByteArrayOutputStream()
        try {
            exec {
                commandLine 'git', 'log', '-1', '--pretty=tformat:%s'
                standardOutput = stdout
            }
        }catch(Exception e){
            System.print("SKIPPING CURSE RELEASE - FAILED TO RETRIEVE LAST COMMIT")
            e.printStackTrace();
            return 'none';
        }
        String commit = stdout.toString().trim()

        if(commit.startsWith("Release") || commit.startsWith("release")){ System.out.println("FOUND NEW RELEASE - COMMIT: " + commit); return 'release' }
        else if(commit.startsWith("Beta") || commit.startsWith("beta")){ System.out.println("FOUND NEW BETA - COMMIT: " + commit); return 'beta' }
        else if(commit.startsWith("Alpha") || commit.startsWith("alpha")){ System.out.println("FOUND NEW ALPHA - COMMIT: " + commit); return 'alpha' }
        else{System.out.println("SKIPPING CURSE RELEASE - COMMIT: " + commit)}
    }
    System.out.println("SKIPPING CURSE RELEASE - NO CURSE API KEY")
    return 'none';
}

tasks.curseforge.enabled = project.hasProperty("curseforge_key") && !commit_release_type.equals('none')
curseforge {
    if(project.hasProperty("curseforge_key") && !commit_release_type.equals('none')) {
        System.out.println("ATTEMPTING CURSE RELEASE")
        apiKey = project.properties["curseforge_key"]
    }
    project {
        id = curse_project_id

        relations {
            requiredlLibrary 'omlib'
            optionalLibrary 'ompd'
            optionalLibrary 'ender-io'
            optionalLibrary 'redstone-flux'
            optionalLibrary 'thermalexpansion'
        }

        mainArtifact(jar) {
            releaseType = commit_release_type.equals('none') ? 'release' : commit_release_type //throws if not release, beta or alpha
            changelog = file("${projectDir}/change_log.html").text
            changelogType = 'html'
        }

        addArtifact(sourceJar){
            changelog = file("${projectDir}/change_log.html").tex
            releaseType = 'alpha'
        }

        addArtifact(deobfJar){
            changelog = file("${projectDir}/change_log.html").tex
            releaseType = 'alpha'
        }
    }
}
