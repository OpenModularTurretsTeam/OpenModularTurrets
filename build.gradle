buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
     classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
 
apply plugin: 'forge'
 
ext.mcversion="1.7.10"
version = "1.3.8"
group= "openmodularturrets" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "OpenModularTurrets"
 
if (hasProperty('appendBuildNumber')) {
    version = version + "-" + appendBuildNumber.toString()
}

version = ext.mcversion + "-" + version // should give something like 1.7.10-1.2.5-99
 
minecraft {
    version = "1.7.10-10.13.2.1291"
    runDir = "eclipse"
}
 
processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        name = "IGW"
        url = "http://maven.k-4u.nl/"
    maven {
        name = "OC repo"
        url = "http://maven.cil.li/"
    }
    }
}

dependencies {
    compile "igwmod:IGW-Mod-1.7.10:1.1.1-17:userdev"
    compile "li.cil.oc:OpenComputers:MC1.7.10-1.5.9.21:dev"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn sourceJar, deobfJar

// this fixes dev environments for IDEA 14 users.
sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}